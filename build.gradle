plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

apply plugin: 'java'
group 'github.saukiya'
version '4.0.0' + '-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()

    //Github Project
    maven { url "https://jitpack.io" }
    //MythicMobs
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    //bStats-bukkit
    maven { url "https://repo.codemc.io/repository/maven-public" }
}

dependencies {
    compileOnly fileTree('libs/spigot-1.14')
    compileOnly fileTree('lib')
    compileOnly 'org.openjdk.nashorn:nashorn-core:15.3'
    compileOnly 'io.lumine.xikage:MythicMobs:4.9.1'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.9'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.github.Saukiya:SX-Item:3.1.6'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly fileTree('libs/spigot-1.14')
    testImplementation 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
}

shadowJar {
    // 过滤未使用的依赖
    minimize()
    // bStats重定向
    relocate('org.bstats', 'github.saukiya.sxitem.bstats')
}

//构建并拷贝
task buildLastJar(type: Copy) {
    dependsOn shadowJar
    from shadowJar.archiveFile.get().asFile.path
    into("$rootDir").rename { "${project.name}-Latest.jar" }
}

//gradle 需要对文件中含有的'$'进行转义: $ -> \$
processResources {
    expand(version: version)
}