plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'java'
group 'github.saukiya'
version '3.8.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    mavenCentral()

    // Minecraft Api
    maven { url 'https://libraries.minecraft.net' }
    // MythicMobs
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    // bStats-bukkit
    maven { url "https://repo.codemc.io/repository/maven-public" }
    // CraftBukkit
    maven { url 'https://repo.codemc.io/repository/nms/' }
    // Github Project
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    compileOnly fileTree('lib')

    compileOnly 'org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'com.github.Saukiya:SX-Item:4.4.0'
    compileOnly 'io.lumine.xikage:MythicMobs:4.9.1'
    compileOnly 'io.lumine:Mythic-Dist:5.2.1'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.9'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

shadowJar {
    // 过滤未使用的依赖
    minimize()
    // bStats重定向
    relocate('org.bstats', 'github.saukiya.sxattribute.bstats')
}

tasks.build.finalizedBy shadowJar
//gradle 需要对文件中含有的'$'进行转义: $ -> \$
processResources.expand(version: version)
tasks.build.finalizedBy shadowJar
buildCopyTask('buildLatestJar', "$rootDir\\build", "Latest")

assemble.group = buildDependents.group = buildNeeded.group = jar.group = classes.group = testClasses.group = "build other"

def buildCopyTask(String taskName, String destinationDir, String jarSuffix) {
    tasks.register(taskName, Copy) {
        group = "build"
        dependsOn shadowJar
        from shadowJar.archiveFile.get().asFile.path
        into(destinationDir).rename { "${project.name}-${jarSuffix}.jar" }
        doLast {
            println "OutputPath: " + destinationDir
        }
    }
}